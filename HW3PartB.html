<!DOCTYPE html>
<html lang="en">
	<head>
		<title>demo - shader</title>
		<meta charset="utf-8">
		<style>
			body {
			  margin: 0px;
				overflow: hidden;
			}
		</style>
	</head>
	<body>

		<div id="container"></div>

    <script src="three.js"></script>

   <script id="vertexShader" type="x-shader/x-vertex">

     	attribute vec3 position;
     	precision mediump float;

	void main()
	{
		gl_Position = vec4( position, 1.0 );
	}

   </script>

<script id="fragmentShader" type="x-shader/x-fragment">
	precision mediump float;
const int MAX_MARCHING_STEPS = 255;
const float MIN_DIST = 0.0;
const float MAX_DIST = 100.0;
const float EPSILON = 0.0001;
uniform vec2 size;
uniform float time;
uniform sampler2D wood_text;




float sphereSDF(vec3 samplePoint) {
    return length(samplePoint) - 1.0;
}

vec3 Twist( vec3 p )
{
    float s = cos((sin(time * 0.5))*p.y * 3.0);
    float c = sin(sin(time * 0.5)*p.y * 3.0);
    mat2  m = mat2(c,-s,s,c);
    return vec3(m*p.xz,p.y);
}

float Sub( float d1, float d2 )
{
    return max(-d1,d2);
}

float sdHexPrism( vec3 p, vec2 h )
{
    vec3 q = abs(p);
    return max(q.z-h.y,max((q.x*0.866025+q.y*0.5),q.y)-h.x);
}

float union1 (float d1, float d2)
{
	return min(d1, d2);
}

float coneSDF( vec3 p,  vec3 c )
{
    vec2 q = vec2( length(p.xz), p.y );
    float d1 = -q.y-c.z;
    float d2 = max( dot(q,c.xy), q.y);
    return length(max(vec2(d1,d2),0.0)) + min(max(d1,d2), 0.);
}

float boxSDF( vec3 p, vec3 b )
{
  return length(max(abs(p)-b,0.0));
}

float sceneSDF(vec3 samplePoint) {
	float scene_float = union1(sphereSDF(samplePoint + vec3(-2.0, 2.0, 0.0)), boxSDF(Twist(samplePoint + vec3(2.0, 2.0, 0.0)), vec3(1.0, 1.0, 1.0)));
	scene_float = union1( scene_float,coneSDF( Twist(samplePoint + vec3(-2.0, -2.0, 0.0)), vec3(0.8,0.2,1.0) ));

     scene_float = union1(scene_float, Sub(sphereSDF(samplePoint + vec3(1.0, sin(time), 15.0)), sdHexPrism(samplePoint + vec3(1.0, -2.0 + sin(time), 0), vec2(1.0, 2.0)) ));

          return scene_float;
}

float shortestDistanceToSurface(vec3 eye, vec3 marchingDirection, float start, float end) {
    float depth = start;
    for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
        float dist = sceneSDF(eye + depth * marchingDirection);
        if (dist < EPSILON) {
			return depth;
        }
        depth += dist;
        if (depth >= end) {
            return end;
        }
    }
    return end;
}
            

vec3 rayDirection(float fieldOfView, vec2 size, vec2 fragCoord) {
    vec2 xy = fragCoord - size / 2.0;
    float z = size.y / tan(radians(fieldOfView) / 2.0);
    return normalize(vec3(xy, -z));
}


vec3 estimateNormal(vec3 p) {
    return normalize(vec3(
        sceneSDF(vec3(p.x + EPSILON, p.y, p.z)) - sceneSDF(vec3(p.x - EPSILON, p.y, p.z)),
        sceneSDF(vec3(p.x, p.y + EPSILON, p.z)) - sceneSDF(vec3(p.x, p.y - EPSILON, p.z)),
        sceneSDF(vec3(p.x, p.y, p.z  + EPSILON)) - sceneSDF(vec3(p.x, p.y, p.z - EPSILON))
    ));
}


vec3 phongContribForLight(vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye,
                          vec3 lightPos, vec3 lightIntensity) {
    vec3 N = estimateNormal(p);
    vec3 L = normalize(lightPos - p);
    vec3 V = normalize(eye - p);
    vec3 R = normalize(reflect(-L, N));
    
    float dotLN = dot(L, N);
    float dotRV = dot(R, V);
    
    if (dotLN < 0.0) {
        // Light not visible from this point on the surface
        return vec3(0.0, 0.0, 0.0);
    } 
    
    if (dotRV < 0.0) {
        // Light reflection in opposite direction as viewer, apply only diffuse
        // component
        return lightIntensity * (k_d * dotLN);
    }
    return lightIntensity * (k_d * dotLN + k_s * pow(dotRV, alpha));
}


vec3 phongIllumination(vec3 k_a, vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye) {
    const vec3 ambientLight = 0.5 * vec3(1.0, 1.0, 1.0);
    vec3 color = ambientLight * k_a;
    
    vec3 light1Pos = vec3(4.0 * sin(time),
                          2.0,
                          4.0 * cos(time));
    vec3 light1Intensity = vec3(0.4, 0.4, 0.4);
    
    color += phongContribForLight(k_d, k_s, alpha, p, eye,
                                  light1Pos,
                                  light1Intensity);
    
    vec3 light2Pos = vec3(2.0 * sin(0.37 * time),
                          2.0 * cos(0.37 * time),
                          2.0);
    vec3 light2Intensity = vec3(0.4, 0.4, 0.4);
    
    color += phongContribForLight(k_d, k_s, alpha, p, eye,
                                  light2Pos,
                                  light2Intensity);    
    return color;
}


void main()
{
	vec4 wood = texture2D(wood_text, vec2( gl_FragCoord.x/size.x, gl_FragCoord.y/size.y));
	vec3 dir = rayDirection(45.0, size, gl_FragCoord.xy);
    vec3 eye = vec3(0.0, 0.0, 17.0);
    float dist = shortestDistanceToSurface(eye, dir, MIN_DIST, MAX_DIST);
    
    if (dist > MAX_DIST - EPSILON) {
        // Didn't hit anything
        gl_FragColor = vec4(0.0,0.0,0.0, 0.0);
		return;
    }
    
    // The closest point on the surface to the eyepoint along the view ray
    vec3 p = eye + dist * dir;
    
    vec3 K_a = vec3(0.2,  0.2, 0.2);
    vec3 K_d = vec3( 0.1, 0.2, 0.2);
    vec3 K_s = vec3(1.0, 1.0, 1.0);
    float shininess = 10.0;
    
    vec3 color = phongIllumination(K_a, K_d, K_s, shininess, p, eye);
        gl_FragColor = vec4(mix(wood.xyz, color.rgb, 0.6), 1.0);
}
</script>

<script>

var material
var scene;
var camera;
var renderer;
var size;
var plane;
var fullScreenQuad;
var vs = document.getElementById( 'vertexShader' ).textContent;
var fs = document.getElementById( 'fragmentShader' ).textContent;
var wood_text = new THREE.TextureLoader().load( 'wood.jpg' );

scene_setup();

function scene_setup(){
	//This is the basic scene setup
	scene = new THREE.Scene();
	var width = window.innerWidth;
	var height = window.innerHeight;

	//orthographic camera can be used for 2D
	camera = new THREE.OrthographicCamera( width / -2, width / 2, height / 2, height / -2, 0.1, 1000 );

	renderer = new THREE.WebGLRenderer();
	renderer.setSize( window.innerWidth, window.innerHeight );
	document.body.appendChild( renderer.domElement );
	var uniforms = {
			time: { type: "f", value: performance.now()},
			size : {type: "v2", value: new THREE.Vector2( width, height)},
			wood_text: { type: "t", value: wood_text  }
		};

	material = new THREE.RawShaderMaterial({
		uniforms: uniforms ,
		vertexShader: vs,
		fragmentShader: fs,
	});

	plane = new THREE.PlaneGeometry( window.innerWidth, window.innerHeight );
	fullScreenQuad = new THREE.Mesh( plane,material);
	scene.add(fullScreenQuad);
}

render();

function render() {

	requestAnimationFrame( render );
	material.uniforms.time.value = performance.now() / 800; 
	renderer.render( scene, camera );
}

</script>

</body>
</html>
